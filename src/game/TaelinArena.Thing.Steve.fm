import TaelinArena.Game

enum
| STEVE_IDLE
| STEVE_DIAMON_SWORD
| STEVE_TNT
| STEVE_ENDER_PEARL
| STEVE_ENDER_DRAGONS_FURY
| STEVE_BLOCK
| STEVE_TAUNT

steve_fun(self: Thing) : Thing
  let self = self <= thing(mov = 3, mhp = 24)

  case self |thing switch self.act

  | STEVE_IDLE
    if is_walking(self)
    then animate(self, 1, STEVE_WALK_000, 8, 16)
    else animate(self, 1, STEVE_IDLE_000, 1, 2)

  // Left 
  | STEVE_DIAMON_SWORD
    let self = animate(self, 1, STEVE_DIAMOND_SWORD_000, 5, 10)
    let hits = [hit([damage(2)], at_dist(self, 15), self.dir, cbox(12))]
    let self = cast(self, 8, hits)
    self 

  // Middle
  | STEVE_TNT
    let self = animate(self, 0, STEVE_TNT_CASTING_000, 3, 9)
    let tnt = new_thing <= thing(fun=steve_tnt_effect, box=nbox)
    let self = spawn(self, 5, [move(tnt, at_max_dist(self, 100))])
    self

  // Right
  | STEVE_ENDER_PEARL
    let self = animate_with_blink(self, 0, STEVE_ENDER_PEARL_000, STEVE_IDLE_000, 10, 0, 20)
    let self = blink(self, 20, at_dist(self, 100))
    self

  // Q
  | STEVE_ENDER_DRAGONS_FURY
    let self = animate(self, 0, STEVE_ENDER_DRAGONS_FURY_CASTING_000, 16, 32)
    let effect = new_thing <= thing(fun=steve_ender_dragons_fury_effect)
    let effect = effect <= thing(dir = targ_dir(self))
    let self = spawn(self, 16, [move(effect, self.trg)])
    self

  // Space
  | STEVE_BLOCK
    let self = animate(self, 0, STEVE_BLOCK_000, 17, 17)
    let block = new_thing <= thing(fun=steve_block_effect)
    let self = spawn(self, 1, [move(block, self.trg)])
    self

  // E
  | STEVE_TAUNT
    animate(self, 0, STEVE_TAUNT_000, 28, 28)

  else self

steve_tnt_effect(self: Thing) : Thing
  case self |thing 
  let self = animate_die(self, 0, STEVE_TNT_000, 18, 36)
  let hits = [hit([damage(2)], self.pos, self.dir, cbox(30))]
  let self = cast(self, 26, hits)
  self

steve_ender_dragons_fury_effect(self: Thing) : Thing
  let self = animate_die(self, 0, STEVE_ENDER_DRAGONS_FURY_000, 13, 26)
  self

steve_block_effect(self: Thing) : Thing
  let self = animate_die(self, 0, STEVE_BLOCK_CASTING_000, 10, 100)
  self
